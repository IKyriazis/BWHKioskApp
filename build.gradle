import com.diffplug.spotless.LineEnding

plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'com.diffplug.gradle.spotless' version '3.27.1'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id "io.freefair.lombok" version "5.0.0-rc6"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

mainClassName = 'edu.wpi.cs3733.d20.teamA.Main'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://apisite.crmyers.dev/maven'
    }
}

run {
    jvmArgs = ['--add-opens=javafx.base/com.sun.javafx.runtime=ALL-UNNAMED',
               '--add-opens=javafx.controls/com.sun.javafx.scene.control.behavior=ALL-UNNAMED',
               '--add-opens=javafx.controls/com.sun.javafx.scene.control=ALL-UNNAMED',
               '--add-opens=javafx.base/com.sun.javafx.binding=ALL-UNNAMED',
               '--add-opens=javafx.base/com.sun.javafx.event=ALL-UNNAMED',
               '--add-opens=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED',
               '--add-opens=javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED',
                ''
    ]
}

dependencies {
    implementation(
            'com.jfoenix:jfoenix:9.0.9',

            // You may comment out the database dependency you do not use
            'org.xerial:sqlite-jdbc:3.30.1',
            'org.apache.derby:derby:10.14.2.0',

            'org.slf4j:slf4j-api:1.7.30',
            'org.slf4j:slf4j-simple:1.7.30',

            'com.opencsv:opencsv:5.1',

            'org.kordamp.ikonli:ikonli-core:11.4.0',
            'org.kordamp.ikonli:ikonli-javafx:11.4.0',
            'org.kordamp.ikonli:ikonli-fontawesome5-pack:11.4.0',
            'org.kordamp.ikonli:ikonli-material-pack:11.4.0',

            'com.google.inject:guice:4.2.2',

            'com.google.zxing:core:3.4.0',
            'com.google.zxing:javase:3.4.0',


            // bcrypt
            'at.favre.lib:bcrypt:0.9.0',

            // batik
            'org.apache.xmlgraphics:batik-rasterizer:1.12',

            // AnimateFX
            'io.github.typhon0:AnimateFX:1.2.1',

            // stuff for google authenticator
            'de.taimos:totp:1.0',
            'commons-codec:commons-codec:1.10',

            //stuff for OpenWeatherMaps API
            'org.jetbrains:annotations:13.0',
            'com.squareup.retrofit2:converter-gson:2.3.0',
            'com.google.code.gson:gson:2.8.2',
            'org.jetbrains.kotlin:kotlin-stdlib:1.1.51',
            'org.jetbrains.kotlin:kotlin-stdlib-jre7:1.1.51',
            'com.squareup.okhttp3:okhttp:3.8.0',
            'com.squareup.okio:okio:1.13.0',
            'com.squareup.retrofit2:retrofit:2.3.0',
            'net.aksingh:owm-japis:2.5.2.1',


            // for comms with arduino
            'com.fazecast:jSerialComm:2.6.2'
    )


    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.6.0',
            'org.testfx:testfx-junit5:4.0.16-alpha',
            'org.mockito:mockito-core:3.3.3',
            'org.mockito:mockito-junit-jupiter:3.3.3',
    )
    testRuntimeOnly(
            'org.testfx:openjfx-monocle:jdk-11+26',
    )
}

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.graphics', 'javafx.media']
}


test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
	
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/services/org.kordamp.ikonli.IkonHandler'
    }
	
	from('src/main/resources/edu/wpi/cs3733/d20/teamA/fuckikonli') {
		include 'META-INF/services/org.kordamp.ikonli.IkonHandler'
	}
}

// Exclude trivial App / Main classes, and UI-related classes b/c Prof. Wong advised us to not waste time on lengthy and
// convoluted TestFX tests
def codeCoverageExcludes = [
        'edu.wpi.cs3733.d20.teamA.App',
        'edu.wpi.cs3733.d20.teamA.Main',
        'edu.wpi.cs3733.d20.teamA.controllers.*',
        'edu.wpi.cs3733.d20.teamA.controls.*',
        'edu.wpi.cs3733.d20.teamA.map.MapCanvas',
        'edu.wpi.cs3733.d20.teamA.util.*',
        'edu.wpi.cs3733.d20.teamA.database.flower.*'
]

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
            excludes = codeCoverageExcludes
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.20
            }
            excludes = codeCoverageExcludes
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.20
            }
            excludes = codeCoverageExcludes
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport
compileJava.dependsOn 'spotlessApply'


// uncomment to enable spotless checking
spotless {
    java {
        googleJavaFormat()

        lineEndings(LineEnding.PLATFORM_NATIVE)
    }
}